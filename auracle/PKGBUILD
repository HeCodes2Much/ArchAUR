# Maintainer: Leonidas Spyropoulos <artafinde at gmail dot com>
# shellcheck disable=all

pkgname=auracle
pkgver=2023.05.17
pkgrel=1
pkgdesc='A flexible client for the AUR'
arch=('x86_64' 'i686')
url="https://github.com/inglor/auracle.git"
license=('MIT')
depends=('pacman' 'libcurl.so' 'libsystemd')
makedepends=('meson' 'git' 'perl' 'systemd')
checkdepends=('python' 'fakechroot' 'gtest')
provides=("$pkgname")
conflicts=("$pkgname-git")
source=("git+https://github.com/inglor/auracle.git")
b2sums=('SKIP')

prepare() {
  cd "$pkgname"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      echo "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
    cd "$pkgname"

    local meson_args=(
        --prefix=/usr
        --buildtype=plain
        --default-library=static
    )

    [[ -d build ]] && meson_args+=(--wipe)

    # Some tests fail with these enabled
    CFLAGS=${CFLAGS/,-D_GLIBCXX_ASSERTIONS/}
    CXXFLAGS="${CFLAGS}"
    meson build "${meson_args[@]}"

    meson compile -C build
}

check() {
    cd "$pkgname"

    meson test -C build
}

package() {
    cd "$pkgname"

    DESTDIR="$pkgdir" meson install -C build
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: et ts=2 sw=2
