# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Ingo BÃ¼rk <admin at airblader dot de>
# shellcheck disable=all

pkgname=i3-gaps
pkgver=4.21
pkgrel=1
pkgdesc='A fork of i3wm tiling window manager with more features, including gaps'
arch=('i686' 'x86_64')
url='https://github.com/Airblader/i3'
license=('BSD')
provides=('i3-wm')
conflicts=('i3-wm')
groups=('i3')
depends=('xcb-util-keysyms' 'xcb-util-wm' 'libev' 'yajl'
    'startup-notification' 'pango' 'perl' 'xcb-util-cursor' 'xcb-util-xrm'
    'libxkbcommon-x11')
makedepends=('bison' 'flex' 'asciidoc' 'xmlto' 'meson')
optdepends=('rxvt-unicode: The terminal emulator used in the default config.'
    'dmenu: As menu.'
    'i3lock: For locking your screen.'
    'i3status: To display system information with a bar.'
    'perl-json-xs: For i3-save-tree'
    'perl-anyevent-i3: For i3-save-tree')
backup=(etc/i3/config)
options=('docs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Airblader/i3/archive/${pkgver}.tar.gz" "meson.build")
sha256sums=('13b87cb4158f51fcbe9f21a6872c30aba682109f2c34db16c6bbf57ea76c89d6' '506dc27338e963d5867f267cc19cf9245e258df8bf55fa8b17752b5222ed0dfb')

build() {
    cp -rf meson.build i3-${pkgver}/meson.build
    cd "i3-${pkgver}"
    arch-meson \
        -Ddocs=true \
        -Dmans=true \
        ../build
    meson compile -C ../build
}

package() {
    cd "i3-${pkgver}"
    DESTDIR="$pkgdir" meson install -C ../build
    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}

# vim:set ts=2 sw=2 et:
