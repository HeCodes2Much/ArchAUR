# Maintainer: The-Repo-Club <The-Repo-Club@github.com>
# Contributor: The-Repo-Club <The-Repo-Club@github.com>
# shellcheck disable=all

pkgname=multimonitorlock
pkgver=2021.08.20
pkgrel=1
pkgdesc="A simple lock script for i3lock-color"
arch=('any')
groups=('linuxrepos')
url="https://github.com/The-Repo-Club/${pkgname}"
license=('GPL3')
depends=('i3lock-color' 'imagemagick' 'xorg-xrandr' 'xorg-xdpyinfo' 'bc' 'bash')
optdepends=('feh: Allows setting wallpaper' 'multimonitorlock-gui: Allows the background to be changed')
makedepends=('git')
conflicts=('multilockscreen-git' 'betterlockscreen' 'betterlockscreen-git')
source=("${pkgname}-$pkgver.tar.gz::${url}/archive/$pkgver.tar.gz")
sha256sums=("8044c18f4505545c2b2466b70ed815c5fb0a5a870e9d867cf090a2eeab80adfa")
install=${pkgname}.install

package() {
    cd "$pkgname-$pkgver"
    install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm 644 "system/$pkgname@.service" "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
    install -Dm 644 "examples/config" "$pkgdir/usr/share/doc/$pkgname/examples/config"
    install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
