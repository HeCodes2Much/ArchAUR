# Maintainer: The-Repo-Club <The-Repo-Club@github.com>
# Contributor: The-Repo-Club <The-Repo-Club@github.com>
# shellcheck disable=all

pkgname=repowm
pkgver=2023.08.06
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="https://github.com/The-Repo-Club/$pkgname"
arch=('i686' 'x86_64')
groups=('linuxrepos')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
optdepends=('repo-exec: exec for repowm'
  'rofi: for the menu scripts'
  'siji: font used by default'
  'nerd-fonts-fira-code: font used by default'
  'adobe-source-code-pro-fonts: font used by default'
  'ttf-material-design-icons: font used by default'
  'ttf-remixicon: font used by default')
install=$pkgname.install
source=("${pkgname}-$pkgver.tar.gz::${url}/archive/$pkgver.tar.gz")
sha256sums=('a09e8b1bbb9b0cbdcb9aaa231184796e8368d1fb426b569d7041fe21e244e6e5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11 \
    FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
