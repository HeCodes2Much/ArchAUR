# Maintainer: The-Repo-Club <The-Repo-Club@github.com>
# Contributor: The-Repo-Club <The-Repo-Club@github.com>
# shellcheck disable=all

pkgname=repowm
pkgver=2022.07.17
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="https://github.com/The-Repo-Club/$pkgname"
arch=('i686' 'x86_64')
groups=('linuxrepos')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'repomenu')
optdepends=('repo-exec: exec for repowm'
  'siji: font used by default'
  'nerd-fonts-fira-code: font used by default'
  'adobe-source-code-pro-fonts: font used by default'
  'ttf-material-design-icons: font used by default'
  'ttf-remixicon: font used by default')
install=$pkgname.install
source=("${pkgname}-$pkgver.tar.gz::${url}/archive/$pkgver.tar.gz")
sha256sums=('1c69cf32c8929c1c6e0c1ce4130854811c1053e8d1bb2c2a3ed165b28d7cd412')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11 \
    FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
